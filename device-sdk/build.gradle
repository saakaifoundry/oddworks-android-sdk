apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

ext {
    bintrayRepo = 'maven'
    bintrayName = 'device-sdk'

    publishedGroupId = 'io.oddworks'
    libraryName = 'Device SDK'
    artifact = 'device-sdk'

    libraryDescription = 'Oddworks Device SDK for Android'

    siteUrl = 'http://android.guide.oddnetworks.com'
    gitUrl = 'https://github.com/oddnetworks/OddworksDeviceSDK'

    libraryVersion = '1.4.0'

    developerId = 'brkattk'
    developerName = 'Erik Straub, Dan Pallas'
    developerEmail = 'erik.straub@oddnetworks.com, dan.pallas@oddnetworks.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ['Apache-2.0']
}

group = publishedGroupId    // Maven Group ID for the artifact
version = libraryVersion    // Version number for the artifact

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                groupId publishedGroupId
                artifactId artifact

                // Add your description here
                name libraryName
                description libraryDescription
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name licenseName
                        url licenseUrl
                    }
                }
                developers {
                    developer {
                        id developerId
                        name developerName
                        email developerEmail
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}


android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName libraryVersion
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile 'com.squareup:otto:1.3.8'
    compile 'net.danlew:android.joda:2.9.1'
    compile 'com.android.support:support-annotations:23.2.0'
    compile "io.reactivex:rxandroid:1.1.0"

    androidTestCompile 'com.android.support.test:runner:0.4'
    androidTestCompile 'com.android.support.test:rules:0.4'
    androidTestCompile 'org.skyscreamer:jsonassert:1.2.3'
    androidTestCompile 'org.hamcrest:hamcrest-core:1.3'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task copyDocs(type: Copy, dependsOn: javadoc) {
    from 'build/docs/javadoc'
    into "../docs/docs/javadoc/$version"
}

artifacts {
    archives javadocJar
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        userOrg = 'oddnetworks'
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        publish = true
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = System.getenv('GPG_PASSWORD')
                //Optional. The passphrase for GPG signing'
            }
        }
    }
}
